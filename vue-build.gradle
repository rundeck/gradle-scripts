ext.spaBuildDir = "$buildDir/spa"

task runNpmBuild(type: NpmTask, group: 'build') {
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    inputs.dir 'src'

    outputs.dir(file("$spaBuildDir"))
    outputs.cacheIf { true }

    def npmCommand = System.env.CI ?
        'ci:build' :
        'dev:build'

    args = ['run', npmCommand]
}
assemble.dependsOn runNpmBuild

task runNpmTest(type: NpmTask, group: 'test') {
    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    if(file("config").isDirectory()) {
        inputs.dir "config"
    }
    inputs.dir 'src'

    def npmCommand = System.env.CI ?
        'ci:test' :
        'dev:test'

    args = ['run', 'ci:test:unit']
}
check.dependsOn runNpmTest

//can package spa contents into a zip
task packageSpa(type: Zip, group: 'build') {
    dependsOn runNpmBuild
    inputs.dir file(spaBuildDir)
    version = project.version

    from file(spaBuildDir)
    include '**/*.js', '**/*.css', '**/*.map', '**/*.png', '**/*.jpg'
}

artifacts {
    archives file: file(spaBuildDir), name: "${project.name}", type: 'directory', builtBy: runNpmBuild
}
